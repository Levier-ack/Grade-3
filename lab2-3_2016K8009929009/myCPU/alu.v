`timescale 10 ns / 1 ns

`define DATA_WIDTH 32

module alu(
    input [31:0] A,
    input [31:0] B,
    input [3:0] ALUop,
    
    output [31:0] Result
);

    wire op_add;//åŠ æ³•
    wire op_sub;//å‡æ³•
    wire op_slt;//æœ‰ç¬¦å·æ¯”ï¿???
    wire op_sltu;//æ— ç¬¦å·æ¯”ï¿???
    wire op_and;//æŒ‰ä½ï¿???
    wire op_nor;//æŒ‰ä½æˆ–é
    wire op_or;//æŒ‰ä½ï¿???
    wire op_xor;//æŒ‰ä½å¼‚æˆ–
    wire op_sll;//é€»è¾‘å·¦ç§»
    wire op_srl;//é€»è¾‘å³ç§»
    wire op_sra;//ç®—æœ¯å³ç§»
    wire op_lui;//é«˜ä½åŠ è½½
    wire no_inst;

//ALUæ“ä½œ
    assign op_add  = (ALUop == 4'd0);
    assign op_sub  = (ALUop == 4'd1);
    assign op_slt  = (ALUop == 4'd2);
    assign op_sltu = (ALUop == 4'd3);
    assign op_and  = (ALUop == 4'd4);
    assign op_nor  = (ALUop == 4'd5);
    assign op_or   = (ALUop == 4'd6);
    assign op_xor  = (ALUop == 4'd7);
    assign op_sll  = (ALUop == 4'd8);
    assign op_srl  = (ALUop == 4'd9);
    assign op_sra  = (ALUop == 4'd10);
    assign op_lui  = (ALUop == 4'd11);
    assign no_inst = (ALUop[3] & ALUop[2]);
//resultå¯„å­˜ï¿???
    wire [31:0] add_sub_result;
    wire [31:0] slt_result;
    wire [31:0] sltu_result;
    wire [31:0] and_result;
    wire [31:0] nor_result;
    wire [31:0] or_result;
    wire [31:0] xor_result;
    wire [31:0] sll_result;
    wire [63:0] sr64_result;
    wire [31:0] sr_result;
    wire [31:0] lui_result;

//é€»è¾‘è¿ç®—
    assign and_result = A & B;
    assign or_result  = A | B;
    assign nor_result = ~(A | B);
    assign xor_result = A ^ B;
    assign lui_result = {B[15:0], 16'b0}; 

//åŠ å‡è¿ç®—
    wire [32:0] adder_a;
    wire [32:0] adder_b;
    wire adder_cin;
    wire [31:0] adder_result;
    wire adder_cout;

    assign adder_a = A;
    assign adder_b = B ^ {32{op_sub | op_slt | op_sltu}};
    assign adder_cin = op_sub|op_slt|op_sltu;
    assign {adder_cout,adder_result} = adder_a + adder_b + adder_cin;
//åŠ å‡ç»“æœ
    assign add_sub_result = adder_result;
//æ¯”è¾ƒç»“æœ
    assign slt_result[31:1] = 31'b0;
    assign slt_result[0] = (A[31]&~B[31]) | (~(A[31]^B[31])) & adder_result[31];

    assign sltu_result[31:1] = 31'b0;
    assign sltu_result[0] = ~adder_cout;

//å·¦ç§»
    assign sll_result = B << A[4:0];
//å³ç§»
    assign sr64_result = {{32{op_sra & B[31]}},B[31:0]} >> A[4:0]; 
    assign sr_result = sr64_result[31:0];

//ç»“æœè¾“å‡º
    assign Result = ({32{op_add | op_sub}} & add_sub_result)
                |({32{op_slt}}         & slt_result)
                |({32{op_sltu}}        & sltu_result)
                |({32{op_and}}         & and_result)
                |({32{op_nor}}         & nor_result)
                |({32{op_or}}          & or_result)
                |({32{op_xor}}         & xor_result)
                |({32{op_sll}}         & sll_result)
                |({32{op_srl|op_sra}}  & sr_result)
                |({32{op_lui}}         & lui_result)
                |({32{no_inst}});

endmodule 